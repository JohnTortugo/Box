dnl // Process this file with autoconf to produce a configure script.

AC_PREREQ(2.50)
AC_INIT(bochs.h)
AC_REVISION([[$Id: configure.in 11364 2012-08-23 19:31:33Z sshwarts $]])
AC_CONFIG_HEADER(config.h)
#AC_CONFIG_HEADER(ltdlconf.h)

dnl // Put Bochs version information right here so that it gets substituted
dnl // into all the right places.
VERSION="0.1.pre"
VER_STRING="0.1.pre"
dnl // WIN_VER_STRING format is "a, b, c, d"
dnl // c should be > 90 for pre-release of next version otherwise patch level
dnl // d should be 0 for release and 1 for svn version
WIN_VER_STRING="2, 5, 91, 1"
REL_STRING="Built from snapshot"

changequote(<<, >>)
changequote([, ])

dnl Detect host and target
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AC_MSG_CHECKING(if you are configuring for another platform)
if test "$cross_compiling" = yes -o "$target_os" = "windows"; then
  AC_MSG_RESULT(yes)
  cross_configure=1
else
  AC_MSG_RESULT(no)
  cross_configure=0
fi

# this case statement defines the compile flags which are needed to
# compile bochs on a platform.  Don't put things like optimization settings
# into the configure.in file, since people will want to be able to change
# those settings by defining CFLAGS and CXXFLAGS before running configure.
NO_LT=0
MSVC_TARGET=0
need_dlcompat_for_plugins=0
case "$target" in
  *-pc-cygwin*)
    NO_LT=1   # do not use libtool at all on cygwin
    if test "$with_term" = yes; then
      # ncurses won't compile with -mno-cygwin or -DWIN32
      # also, I can't get it to link without this -DBROKEN_LINKER=1 hack.
      # see /usr/include/curses.h for details.
      ADD_FLAGS="-DBROKEN_LINKER=1"
    fi
    ;;
  *-mingw32*)
    NO_LT=1   # do not use libtool at all on cygwin
    if test "$with_term" = yes; then
      # ncurses won't compile with -mno-cygwin or -DWIN32
      # also, I can't get it to link without this -DBROKEN_LINKER=1 hack.
      # see /usr/include/curses.h for details.
      ADD_FLAGS="-DBROKEN_LINKER=1"
    else
      # default case
      ADD_FLAGS="-DWIN32"   # required for mingw compile+win32 gui
      DEFAULT_GUI=win32     # default to win32 gui
    fi
    ;;
  *-macosx* | *-darwin*)
    ADD_FLAGS="-fpascal-strings -fno-common -Wno-four-char-constants -Wno-unknown-pragmas -Dmacintosh"      # required for macosx compile
    DEFAULT_GUI=nogui                # default to carbon
    need_dlcompat_for_plugins=1
    ;;
  *-macos*)
    DEFAULT_GUI=nogui                 # macos defaults to macos
    ;;
  *-solaris*)
    ADD_FLAGS="-D_XOPEN_SOURCE_EXTENDED=1 -D__EXTENSIONS__"      # required for correct function prototypes
    LIBS="$LIBS -lsocket -lnsl"
    DEFAULT_GUI=nogui
    ;;
  *)
    DEFAULT_GUI=nogui
    ;;
esac
if test "${ADD_FLAGS:+set}" = set; then
      CFLAGS="$CFLAGS $ADD_FLAGS"
      CXXFLAGS="$CXXFLAGS $ADD_FLAGS"
      CPPFLAGS="$CPPFLAGS $ADD_FLAGS"
fi

AC_MSG_CHECKING(for standard CFLAGS on this platform)
AC_MSG_RESULT($ADD_FLAGS)

dnl // make sure X Windows is default if no other chosen
if (test "$with_sdl" != yes) && \
   (test "$with_svga" != yes) && \
   (test "$with_x11" != yes) && \
   (test "$with_win32" != yes) && \
   (test "$with_nogui" != yes) && \
   (test "$with_term" != yes) && \
   (test "$with_rfb" != yes) && \
   (test "$with_amigaos" != yes) && \
   (test "$with_carbon" != yes) && \
   (test "$with_wx" != yes) && \
   (test "$with_macos" != yes); then
  # use DEFAULT_GUI.  Set the appropriate variable.
  # DEFAULT_GUI must be set to one of the names above.  Otherwise, no
  # valid $with_* variable will be set and who knows what will happen?
  eval "with_${DEFAULT_GUI}=yes"
fi

AC_PROG_CC
AC_PROG_CXX
AC_PROG_MAKE_SET

if test "$MSVC_TARGET" != 1; then
  dnl------------ libtool configuration
  dnl Configure libtool, and default to shared libraries.  Libtool will only be
  dnl used for compiling and linking plugins.
  AC_DISABLE_STATIC
  dnl Check for dlopen support
  AC_LIBTOOL_DLOPEN
  dnl Configure libtool
  AC_PROG_LIBTOOL
  dnl Configure the ltdl library.  This must go after AC_PROG_LIBTOOL or
  dnl else it disables shared libraries somehow.
  AC_LIB_LTDL
fi

AC_PATH_PROG(PKGCONFIG, pkg-config, not_found)
if test "$PKGCONFIG" = not_found; then
  AC_PATH_XTRA
fi

AC_C_BIGENDIAN
AC_C_INLINE
AC_CHECK_SIZEOF(unsigned char)
AC_CHECK_SIZEOF(unsigned short)
AC_CHECK_SIZEOF(unsigned int)
AC_CHECK_SIZEOF(unsigned long)
AC_CHECK_SIZEOF(unsigned long long)
AC_CHECK_SIZEOF(int *)
AC_CHECK_FUNCS(getenv, AC_DEFINE(BX_HAVE_GETENV))
AC_CHECK_FUNCS(setenv, AC_DEFINE(BX_HAVE_SETENV))
AC_CHECK_FUNCS(snprintf, AC_DEFINE(BX_HAVE_SNPRINTF))
AC_CHECK_FUNCS(vsnprintf, AC_DEFINE(BX_HAVE_VSNPRINTF))

if test "$MSVC_TARGET" != 1; then
  AC_CHECK_FUNCS(select, AC_DEFINE(BX_HAVE_SELECT))
  AC_CHECK_TYPE(socklen_t, AC_DEFINE(BX_HAVE_SOCKLEN_T), , [#include <sys/types.h>
  #include <sys/socket.h>])
  AC_CHECK_FUNCS(strtoull, AC_DEFINE(BX_HAVE_STRTOULL))
  AC_CHECK_FUNCS(strtouq, AC_DEFINE(BX_HAVE_STRTOUQ))
  AC_CHECK_FUNCS(strdup, AC_DEFINE(BX_HAVE_STRDUP))
  AC_CHECK_FUNCS(strrev, AC_DEFINE(BX_HAVE_STRREV))
  AC_CHECK_FUNCS(stricmp, AC_DEFINE(BX_HAVE_STRICMP))
  AC_CHECK_FUNCS(strcasecmp, AC_DEFINE(BX_HAVE_STRCASECMP))
  AC_CHECK_FUNCS(mkstemp, AC_DEFINE(BX_HAVE_MKSTEMP))
  AC_CHECK_HEADER(sys/mman.h, AC_DEFINE(BX_HAVE_SYS_MMAN_H))
  AC_CHECK_FUNCS(gettimeofday, AC_DEFINE(BX_HAVE_GETTIMEOFDAY))
  AC_CHECK_FUNCS(usleep, AC_DEFINE(BX_HAVE_USLEEP))

  AC_MSG_CHECKING(for __builtin_bswap32)
  AC_TRY_LINK([],[
    __builtin_bswap32(0x12345678);
  ],[
    AC_DEFINE(BX_HAVE___BUILTIN_BSWAP32, 1, [Define to 1 if you have the '__builtin_bswap32' function.])
    AC_MSG_RESULT(yes)
  ],[
    AC_MSG_RESULT(no)
  ])
  AC_MSG_CHECKING(for __builtin_bswap64)
  AC_TRY_LINK([],[
    __builtin_bswap64(0x12345678);
  ],[
    AC_DEFINE(BX_HAVE___BUILTIN_BSWAP64, 1, [Define to 1 if you have the '__builtin_bswap64' function.])
    AC_MSG_RESULT(yes)
  ],[
    AC_MSG_RESULT(no)
  ])
else
  AC_DEFINE(BX_HAVE_SELECT, 1)
  AC_DEFINE(BX_HAVE_SOCKLEN_T, 0)
  AC_DEFINE(BX_HAVE_STRTOULL, 1)
  AC_DEFINE(BX_HAVE_STRTOUQ, 0)
  AC_DEFINE(BX_HAVE_STRDUP, 1)
  AC_DEFINE(BX_HAVE_STRREV, 1)
  AC_DEFINE(BX_HAVE_STRICMP, 1)
  AC_DEFINE(BX_HAVE_STRCASECMP, 0)
  AC_DEFINE(BX_HAVE_MKSTEMP, 0)
  AC_DEFINE(BX_HAVE_SYS_MMAN_H, 0)
  AC_DEFINE(BX_HAVE_GETTIMEOFDAY, 0)
  AC_DEFINE(BX_HAVE_USLEEP, 0)
  AC_DEFINE(BX_HAVE___BUILTIN_BSWAP32, 0)
  AC_DEFINE(BX_HAVE___BUILTIN_BSWAP64, 0)
fi
AC_CHECK_FUNCS(sleep, AC_DEFINE(BX_HAVE_SLEEP))
AC_CHECK_FUNCS(nanosleep, AC_DEFINE(BX_HAVE_NANOSLEEP))
AC_CHECK_FUNCS(abort, AC_DEFINE(BX_HAVE_ABORT))
AC_CHECK_MEMBER(struct sockaddr_in.sin_len, AC_DEFINE(BX_HAVE_SOCKADDR_IN_SIN_LEN), , [#include <sys/socket.h>
#include <netinet/in.h> ])
AC_CHECK_FUNCS(timelocal, AC_DEFINE(BX_HAVE_TIMELOCAL))
AC_CHECK_FUNCS(gmtime, AC_DEFINE(BX_HAVE_GMTIME))
AC_CHECK_FUNCS(mktime, AC_DEFINE(BX_HAVE_MKTIME))
AC_CHECK_FUNCS(tmpfile64, AC_DEFINE(BX_HAVE_TMPFILE64))
AC_CHECK_FUNCS(fseek64, AC_DEFINE(BX_HAVE_FSEEK64))
AC_CHECK_FUNCS(fseeko64, AC_DEFINE(BX_HAVE_FSEEKO64))

dnl As of autoconf 2.53, the standard largefile test fails for Linux/gcc.
dnl It does not put the largefiles arguments into CFLAGS, even though Linux/gcc
dnl does need them.  So we do it ourselves.
AC_SYS_LARGEFILE

dnl we need to define _FILE_OFFSET_BITS or _LARGE_FILES on the compiler command
dnl line because otherwise the system headers risk being included before
dnl config.h which defines these constants leading to inconsistent
dnl sizeof(off_t) in different source files of the same program and linking
dnl problems
case "x$ac_cv_sys_file_offset_bits" in #(
  xno | xunknown) ;;
  *)
    if test "x$ac_cv_sys_file_offset_bits" = "x64"; then
        BX_LARGEFILE_FLAGS="-D_FILE_OFFSET_BITS=64 -D_LARGE_FILES"
    else
        BX_LARGEFILE_FLAGS="-D_LARGE_FILES"
    fi
    CPPFLAGS="$CPPFLAGS $BX_LARGEFILE_FLAGS"
    CFLAGS="$CFLAGS $BX_LARGEFILE_FLAGS"
    CXXFLAGS="$CXXFLAGS $BX_LARGEFILE_FLAGS"
    ;;
esac
dnl ---end of largefile tests

dnl Add the -lm library if math functions cannot be used without it.
dnl This check is important on cygwin because of the bizarre way that they
dnl have organized functions into libraries.  On cygwin, both libc.a and
dnl libm.a are symbolic links to a single lib libcygwin.a.  This means that
dnl   1) linking with -lm is not necessary, and
dnl   2) linking with -lm is dangerous if the order of libraries is wrong
dnl In particular, if you compile any program with -mno-cygwin and link with
dnl -lm, it will crash instantly when it is run.  This happens because the
dnl linker incorrectly links the Cygwin libm.a (==libcygwin.a), which replaces
dnl the ___main function instead of allowing it to be defined by
dnl /usr/lib/mingw/libmingw32.a as it should be.
dnl
dnl On MacOS X, this test will find that -lm is unnecessary and leave it out.
dnl
dnl Just check a few math functions. If they are all found without
dnl -lm, then we must not need -lm.
have_cos=0
have_floor=0
AC_CHECK_FUNCS(cos, have_cos=1)
AC_CHECK_FUNCS(floor, have_floor=1)
AC_MSG_CHECKING(if math functions link without -lm)
if test "$have_cos" = 1 -a "$have_floor" = 1; then
    AC_MSG_RESULT(yes)
else
    AC_MSG_RESULT(no)
    LIBS="$LIBS -lm"
    # use different functions to bypass configure caching
    have_sin=0
    have_ceil=0
    AC_CHECK_FUNCS(sin, have_sin=1)
    AC_CHECK_FUNCS(ceil, have_ceil=1)
    AC_MSG_CHECKING(if math functions link with -lm)
    if test "$have_sin" = 1 -a "$have_ceil" = 1; then
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
        # not sure we should warn the user, crash, etc.
	# expect link failure
    fi
fi

AC_MSG_CHECKING(if compiler allows __attribute__)
AC_TRY_COMPILE([], [typedef struct { } __attribute__ ((packed)) junk;],
    AC_MSG_RESULT(yes),
    [
      AC_MSG_RESULT(no)
      AC_DEFINE(BX_NO_ATTRIBUTES)
    ])

AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING(for set)
AC_TRY_COMPILE([#include <set>], [],
   [
     AC_MSG_RESULT(yes)
     AC_DEFINE(BX_HAVE_SET)
   ], AC_MSG_RESULT(no))
AC_MSG_CHECKING(for set.h)
AC_TRY_COMPILE([#include <set.h>], [],
   [
     AC_MSG_RESULT(yes)
     AC_DEFINE(BX_HAVE_SET_H)
   ], AC_MSG_RESULT(no))
AC_MSG_CHECKING(for map)
AC_TRY_COMPILE([#include <map>], [],
   [
     AC_MSG_RESULT(yes)
     AC_DEFINE(BX_HAVE_MAP)
   ], AC_MSG_RESULT(no))
AC_MSG_CHECKING(for map.h)
AC_TRY_COMPILE([#include <map.h>], [],
   [
     AC_MSG_RESULT(yes)
     AC_DEFINE(BX_HAVE_MAP_H)
   ], AC_MSG_RESULT(no))
AC_LANG_RESTORE

dnl Implement a check for each gui library to see if has a chance of compiling.
if test "$with_all_libs" = yes; then
  if test "$cross_configure" = 1; then
    AC_MSG_WARN([[Using --with-all-libs while cross configuring is very unlikely to be what you want.  You should specify the --with-* args yourself.]])
  fi

  if test "$with_amigaos" != yes; then
    can_compile_amigaos=1
    AC_CHECK_HEADER([proto/intuition.h], [], [ can_compile_amigaos=0 ])
    AC_CHECK_HEADER([intuition/intuitionbase.h], [], [ can_compile_amigaos=0 ])
    AC_CHECK_HEADER([cybergraphx/cybergraphics.h], [], [ can_compile_amigaos=0 ])
    AC_CHECK_HEADER([devices/trackdisk.h], [], [ can_compile_amigaos=0 ])
    if test $can_compile_amigaos = 1; then
      with_amigaos=yes
    fi
  fi

  if test "$with_macos" != yes; then
    can_compile_macos=1
    AC_CHECK_HEADER([Quickdraw.h], [], [ can_compile_macos=0 ])
    AC_CHECK_HEADER([Dialogs.h], [], [ can_compile_macos=0 ])
    if test $can_compile_macos = 1; then
      with_macos=yes
    fi
  fi

  if test "$with_carbon" != yes; then
    can_compile_carbon=1
    AC_CHECK_HEADER([Carbon.h], [], [ can_compile_carbon=0 ])
    AC_CHECK_HEADER([ApplicationServices/ApplicationServices.h], [], [ can_compile_carbon=0 ])
    if test $can_compile_carbon = 1; then
      with_carbon=yes
    fi
  fi

  if test "$with_win32" != yes; then
    can_compile_win32=1
    AC_CHECK_HEADER([windows.h], [], [ can_compile_win32=0 ])
    AC_CHECK_HEADER([commctrl.h], [], [ can_compile_win32=0 ])
    if test $can_compile_win32 = 1; then
      with_win32=yes
    fi
  fi

  if test "$with_sdl" != yes; then
    can_compile_sdl=1
    AC_CHECK_HEADER([SDL/SDL.h], [], [ can_compile_sdl=0 ])
    AC_CHECK_HEADER([SDL/SDL_main.h], [], [ can_compile_sdl=0 ])
    if test $can_compile_sdl = 1; then
      with_sdl=yes
    fi
  fi

  if test "$with_svga" != yes; then
    can_compile_svga=1
    AC_CHECK_HEADER([vga.h], [], [ can_compile_svga=0 ])
    AC_CHECK_HEADER([vgagl.h], [], [ can_compile_svga=0 ])
    if test $can_compile_svga = 1; then
      with_svga=yes
    fi
  fi

  if test "$with_x11" != yes; then
    can_compile_x11=1
    AC_CHECK_HEADER([X11/Xlib.h], [], [ can_compile_x11=0 ])
    AC_CHECK_HEADER([X11/Xutil.h], [], [ can_compile_x11=0 ])
    if test $can_compile_x11 = 1; then
      with_x11=yes
    fi
  fi

  if test "$with_rfb" != yes; then
    can_compile_rfb=1
    case $target in
      *-pc-windows* | *-pc-winnt* | *-pc-cygwin* | *-mingw32*)
	AC_CHECK_HEADER([winsock.h], [], [ can_compile_rfb=0 ])
	AC_CHECK_HEADER([process.h], [], [ can_compile_rfb=0 ])
        ;;
      *)
	AC_CHECK_HEADER([sys/socket.h], [], [ can_compile_rfb=0 ])
	AC_CHECK_HEADER([netinet/tcp.h], [], [ can_compile_rfb=0 ])
	AC_CHECK_HEADER([pthread.h], [], [ can_compile_rfb=0 ])
	;;
    esac
    if test $can_compile_rfb = 1; then
      with_rfb=yes
    fi
  fi

  if test "$with_term" != yes; then
    can_compile_term=1
    AC_CHECK_HEADER([curses.h], [], [ can_compile_term=0 ])
    AC_CHECK_HEADER([signal.h], [], [ can_compile_term=0 ])
    if test $can_compile_term = 1; then
      with_term=yes
    fi
  fi

  if test "$with_nogui" != yes; then
    with_nogui=yes
  fi
fi    # end of if $with_all_libs = yes

AC_MSG_CHECKING(for idle hack)
AC_ARG_ENABLE(idle-hack,
  [  --enable-idle-hack                use Roland Mainz's idle hack],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_USE_IDLE_HACK, 1)
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_USE_IDLE_HACK, 0)
   fi],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_USE_IDLE_HACK, 0)
    ]
  )
AC_SUBST(BX_USE_IDLE_HACK)

AC_CHECK_HEADER(dlfcn.h, AC_DEFINE(BX_HAVE_DLFCN_H, 1))
AC_CHECK_HEADER(assert.h, AC_DEFINE(HAVE_ASSERT_H, 1))

AC_MSG_CHECKING(for plugins support)
AC_ARG_ENABLE(plugins,
  [  --enable-plugins                  enable plugins],
  [if test "$enableval" = yes; then
     bx_plugins=1
   else
     bx_plugins=0
   fi],
  [
    bx_plugins=0
    ]
  )

if test "$bx_plugins" = 1; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(BX_PLUGINS, 1)
  GUI_NON_PLUGIN_OBJS='$(OBJS_THAT_CANNOT_BE_PLUGINS)'
  GUI_PLUGIN_OBJS='$(OBJS_THAT_CAN_BE_PLUGINS)'
  DEFINE_PLUGIN_PATH='-DBX_PLUGIN_PATH="\"${plugdir}\""'
  # and the $(OBJS_THAT_SUPPORT_OTHER_PLUGINS) will be built and linked
  # by special link rules for the plugins that they support.
  LIBS="$LIBS $LIBADD_DL"
  have_ltdl=0
  AC_CHECK_HEADER(ltdl.h, [AC_CHECK_LIB(ltdl, lt_dlinit, [have_ltdl=1]]))
  if test "$have_ltdl" = 1; then
    PLUGIN_VAR="-lltdl"
    AC_DEFINE(BX_HAVE_LTDL,1)
  else
    PLUGIN_VAR="ltdl.o"
  fi
  PLUGIN_TARGET=bochs_plugins
  INSTALL_PLUGINS_VAR=install_libtool_plugins
  NONPLUGIN_GUI_LINK_OPTS=''
else
  AC_MSG_RESULT(no)
  bx_plugins=0
  AC_DEFINE(BX_PLUGINS, 0)
  GUI_NON_PLUGIN_OBJS='$(OBJS_THAT_CANNOT_BE_PLUGINS) $(OBJS_THAT_CAN_BE_PLUGINS) $(OBJS_THAT_SUPPORT_OTHER_PLUGINS)'
  GUI_PLUGIN_OBJS=''
  NONPLUGIN_GUI_LINK_OPTS='$(GUI_LINK_OPTS)'
fi
AC_SUBST(DEFINE_PLUGIN_PATH)
AC_SUBST(NONPLUGIN_GUI_LINK_OPTS)

# copy gui variables into iodev variables.  Later, we will add to the gui
# objs list, according to which display libraries are enabled.
IODEV_NON_PLUGIN_OBJS=$GUI_NON_PLUGIN_OBJS
IODEV_PLUGIN_OBJS=$GUI_PLUGIN_OBJS
IODEV_EXT_NON_PLUGIN_OBJS=$GUI_NON_PLUGIN_OBJS
IODEV_EXT_PLUGIN_OBJS=$GUI_PLUGIN_OBJS

# on MacOSX if they enabled plugins, make sure that dlopen() was found.
# It is provided by a library called dlcompat.
if test "$bx_plugins" = 1 -a "$need_dlcompat_for_plugins" = 1; then
  have_dlopen=0
  AC_CHECK_LIB(dl, dlopen, [have_dlopen=1])
  AC_MSG_CHECKING(if you have dlcompat, required for MacOSX plugins)
  if test "$have_dlopen" = 0; then
    AC_MSG_RESULT(no)
    AC_MSG_ERROR([To use plugins on MacOSX you must use a library called dlcompat.  The configure script was not able to find dlcompat.  If it is already installed then you must set up your environment variables to point to it, as is done in .conf.macosx.  If you cannot resolve this, you should turn off plugins.])
  else
    AC_MSG_RESULT(yes)
  fi
fi

AC_MSG_CHECKING(if compiler allows LL for 64-bit constants)
AC_TRY_COMPILE([], [ { 42LL; } ],
    AC_MSG_RESULT(yes),
    [
      AC_MSG_RESULT(no)
      AC_DEFINE(BX_64BIT_CONSTANTS_USE_LL, 0)
    ])

AC_MSG_CHECKING(for A20 support)
AC_ARG_ENABLE(a20-pin,
  [  --enable-a20-pin                  compile in support for A20 pin],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_A20, 1)
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_A20, 0)
   fi
   ],
  [
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_A20, 1)
    ]
  )

use_x86_64=0
AC_MSG_CHECKING(for x86-64 support)
AC_ARG_ENABLE(x86-64,
  [  --enable-x86-64                   compile in support for x86-64 instructions],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    OBJS64='$(OBJS64)'
    AC_DEFINE(BX_SUPPORT_X86_64, 1)
    use_x86_64=1
   else
    AC_MSG_RESULT(no)
    OBJS64=''
    AC_DEFINE(BX_SUPPORT_X86_64, 0)
   fi
   ],
  [
    AC_MSG_RESULT(no)
    OBJS64=''
    AC_DEFINE(BX_SUPPORT_X86_64, 0)
    ]
  )
AC_SUBST(OBJS64)

AC_DEFINE(BX_SUPPORT_SMP, 0)

AC_DEFINE(BX_CPU_LEVEL, 6)
bx_cpu_level=6

AC_MSG_CHECKING(for larger than 32 bit physical address emulation)
AC_ARG_ENABLE(long-phy-address,
  [  --enable-long-phy-address         compile in support for physical address larger than 32 bit],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_PHY_ADDRESS_LONG, 1)
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_PHY_ADDRESS_LONG, 0)
   fi
   ],
  [
    if test "$bx_cpu_level" -ge 5; then
      AC_MSG_RESULT(yes)
      AC_DEFINE(BX_PHY_ADDRESS_LONG, 1)
    else
      AC_MSG_RESULT(no)
      AC_DEFINE(BX_PHY_ADDRESS_LONG, 0)
    fi
    ]
  )

AC_MSG_CHECKING(for large ramfile support)
AC_ARG_ENABLE(large-ramfile,
  [  --enable-large-ramfile                     enable large ramfile support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_LARGE_RAMFILE, 1)
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_LARGE_RAMFILE, 0)
   fi],
  [
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_LARGE_RAMFILE, 1)
    ]
  )
AC_SUBST(BX_LARGE_RAMFILE)

networking=no
AC_DEFINE(BX_SUPPORT_NE2K, 0)
AC_DEFINE(BX_SUPPORT_PCI, 0)
NETDEV_OBJS=''

#
# PCI host device support
#
AC_MSG_CHECKING(for PCI host device mapping support)
AC_ARG_ENABLE(pcidev,
  [  --enable-pcidev                   enable PCI host device mapping support (linux host only)],
  [
    if test "$enableval" = "yes"; then
      AC_MSG_RESULT(yes)
      if test "$pci" != "1"; then
        AC_MSG_ERROR([PCI host device mapping requires PCI support])
      fi
      case "$target" in
        *-linux*)
          AC_MSG_NOTICE([Linux detected as host for PCI host device mapping])
          linux_version=`uname -r`
	  case "$linux_version" in
		2.4*)
	  		PCIDEV_MODULE_MAKE_ALL="all-kernel24"
			KERNEL_MODULE_SUFFIX="o"
          	;;
		2.6*|3.*)
	  		PCIDEV_MODULE_MAKE_ALL="all-kernel26"
			KERNEL_MODULE_SUFFIX="ko"
		;;
		*)
			AC_MSG_ERROR([Linux kernel 2.4, 2.6 or 3.x is required for PCI host device mapping])
		;;
	  esac
	  KERNELDIR="/lib/modules/$linux_version/build"
	  LSMOD="lsmod"
	  INSMOD="insmod"
	  RMMOD="rmmod"
	  DEPMOD="depmod"
	  CLEAN_PCIDEV_VAR=clean_pcidev
	  AC_SUBST(KERNELDIR)
	  AC_SUBST(LSMOD)
	  AC_SUBST(INSMOD)
	  AC_SUBST(RMMOD)
	  AC_SUBST(DEPMOD)
	  AC_SUBST(PCIDEV_MODULE_MAKE_ALL)
	  AC_SUBST(KERNEL_MODULE_SUFFIX)
	  AC_SUBST(CLEAN_PCIDEV_VAR)
        ;;
        *)
          AC_MSG_ERROR([PCI host device mapping requires Linux as host])
        ;;
      esac
      AC_DEFINE(BX_SUPPORT_PCIDEV, 1)
      PCI_OBJ="$PCI_OBJ pcidev.o"
    else
      AC_MSG_RESULT(no)
      AC_DEFINE(BX_SUPPORT_PCIDEV, 0)
    fi
  ],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_PCIDEV, 0)
  ]
)

use_usb=0
USBHC_OBJS=''
AC_MSG_CHECKING(for USB UHCI support)
AC_ARG_ENABLE(usb,
  [  --enable-usb                      enable USB UHCI support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_USB_UHCI, 1)
    USBHC_OBJS="usb_uhci.o"
    use_usb=1
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_USB_UHCI, 0)
   fi],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_USB_UHCI, 0)
    ]
  )

AC_MSG_CHECKING(for USB OHCI support)
AC_ARG_ENABLE(usb-ohci,
  [  --enable-usb-ohci                 enable USB OHCI support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_USB_OHCI, 1)
    USBHC_OBJS="$USBHC_OBJS usb_ohci.o"
    use_usb=1
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_USB_OHCI, 0)
   fi],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_USB_OHCI, 0)
    ]
  )

AC_MSG_CHECKING(for experimental USB xHCI support)
AC_ARG_ENABLE(usb-xhci,
  [  --enable-usb-xhci                 enable experimental USB xHCI support (incomplete)],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_USB_XHCI, 1)
    USBHC_OBJS="$USBHC_OBJS usb_xhci.o"
    use_usb=1
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_USB_XHCI, 0)
   fi],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_USB_XHCI, 0)
    ]
  )

if test "$use_usb" = 1; then
  USBCORE_OBJ="usb_common.o"
  USBDEV_OBJS="usb_hid.o usb_hub.o usb_msd.o usb_printer.o"
  SCSI_OBJS="scsi_device.o"
  AC_SUBST(USBHC_OBJS)
  AC_SUBST(USBCORE_OBJ)
  AC_SUBST(USBDEV_OBJS)
  AC_SUBST(SCSI_OBJS)
  if test "$bx_plugins" = 0; then
    USB_LIB_VAR='iodev/usb/libusb.a'
    AC_SUBST(USB_LIB_VAR)
  fi
fi

AC_MSG_CHECKING(for PCI pseudo NIC support)
AC_ARG_ENABLE(pnic,
  [  --enable-pnic                     enable PCI pseudo NIC support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_PCIPNIC, 1)
    NETDEV_OBJS="$NETDEV_OBJS pcipnic.o"
    networking=yes
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_PCIPNIC, 0)
   fi],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_PCIPNIC, 0)
    ]
  )

AC_MSG_CHECKING(for Intel(R) Gigabit Ethernet support)
AC_ARG_ENABLE(e1000,
  [  --enable-e1000                    enable Intel(R) Gigabit Ethernet support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_E1000, 1)
    NETDEV_OBJS="$NETDEV_OBJS e1000.o"
    networking=yes
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_E1000, 0)
   fi],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_E1000, 0)
    ]
  )

NETLOW_OBJS=''
if test "$networking" = yes; then
  NETLOW_OBJS='eth_null.o eth_vnet.o'
  if test "$MSVC_TARGET" != 1; then
    AC_CHECK_HEADER(net/bpf.h, [
        NETLOW_OBJS="$NETLOW_OBJS eth_fbsd.o"
        AC_DEFINE(BX_NETMOD_FBSD, 1)
      ])
    AC_CHECK_HEADER(netpacket/packet.h, [
        NETLOW_OBJS="$NETLOW_OBJS eth_linux.o"
        AC_DEFINE(BX_NETMOD_LINUX, 1)
      ])
    AC_CHECK_FUNCS(fork, have_fork=1)
    AC_CHECK_FUNCS(execlp, have_execlp=1)
    AC_CHECK_FUNCS(socketpair, have_socketpair=1)
    if test x"$have_fork" = x1 -a x"$have_execlp" = x1 -a x"$have_socketpair" = x1; then
      NETLOW_OBJS="$NETLOW_OBJS eth_slirp.o"
      AC_DEFINE(BX_NETMOD_SLIRP, 1)
    fi
  fi
  case "$target" in
    *-pc-windows* | *-pc-winnt* | *-pc-cygwin* | *-mingw32*)
    NETLOW_OBJS="$NETLOW_OBJS eth_win32.o"
    AC_DEFINE(BX_NETMOD_WIN32, 1)
    ;;
    *-macosx* | *-darwin*)
    NETLOW_OBJS="$NETLOW_OBJS eth_tuntap.o"
    AC_DEFINE(BX_NETMOD_TUNTAP, 1)
    ;;
    *)
    AC_CHECK_HEADER(net/if.h, [
        use_ethertap=yes
        AC_DEFINE(BX_HAVE_NET_IF_H, 1)
      ], [],
      [
      #include <sys/types.h>
      #include <sys/socket.h>
    ])
    AC_CHECK_HEADER(linux/netlink.h, [
        use_ethertap=yes
      ], [],
      [
      #include <asm/types.h>
      #include <sys/socket.h>
    ])

    if test "$use_ethertap" = yes; then
      NETLOW_OBJS="$NETLOW_OBJS eth_tap.o eth_vde.o"
      AC_DEFINE(BX_NETMOD_TAP, 1)
      AC_DEFINE(BX_NETMOD_VDE, 1)
    fi

    AC_CHECK_HEADER(net/if_tun.h, [
        use_tuntap=yes
      ], [],
      [
      #include <sys/types.h>
      #include <sys/socket.h>
      #include <net/if.h>
    ])
    AC_CHECK_HEADER(linux/if_tun.h, [
        use_tuntap=yes
      ], [],
      [
      #include <asm/types.h>
      #include <sys/socket.h>
    ])
    if test "$use_tuntap" = yes; then
      NETLOW_OBJS="$NETLOW_OBJS eth_tuntap.o"
      AC_DEFINE(BX_NETMOD_TUNTAP, 1)
    fi

    ;;
  esac
  if test "$bx_plugins" = 0; then
    NETWORK_LIB_VAR='iodev/network/libnetwork.a'
    AC_SUBST(NETWORK_LIB_VAR)
  fi
  AC_DEFINE(BX_NETWORKING, 1)
else
  AC_DEFINE(BX_NETWORKING, 0)
fi

AC_SUBST(NETDEV_OBJS)
AC_SUBST(NETLOW_OBJS)

AC_MSG_CHECKING(for repeated IO and mem copy speedups)
AC_ARG_ENABLE(repeat-speedups,
  [  --enable-repeat-speedups          support repeated IO and mem copy speedups],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    speedup_repeat=1
   else
    AC_MSG_RESULT(no)
    speedup_repeat=0
   fi],
  [
    AC_MSG_RESULT(no)
    speedup_repeat=0
    ]
  )

AC_MSG_CHECKING(for gcc fast function calls optimization)
AC_ARG_ENABLE(fast-function-calls,
  [  --enable-fast-function-calls      support for fast function calls (gcc on x86 only)],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    speedup_fastcall=1
   else
    AC_MSG_RESULT(no)
    speedup_fastcall=0
   fi],
  [
    AC_MSG_RESULT(no)
    speedup_fastcall=0
    ]
  )

AC_MSG_CHECKING(for handlers chaining speedups)
AC_ARG_ENABLE(handlers-chaining,
  [  --enable-handlers-chaining        support handlers-chaining emulation speedups],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    speedup_handlers_chaining=1
   else
    AC_MSG_RESULT(no)
    speedup_handlers_chaining=0
   fi],
  [
    AC_MSG_RESULT(no)
    speedup_handlers_chaining=0
    ]
  )

AC_MSG_CHECKING(support for configurable MSR registers)
AC_ARG_ENABLE(configurable-msrs,
  [  --enable-configurable-msrs        support for configurable MSR registers],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_CONFIGURE_MSRS, 1)
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_CONFIGURE_MSRS, 0)
   fi],
  [
    if test "$bx_cpu_level" -ge 5; then
      AC_MSG_RESULT(yes)
      AC_DEFINE(BX_CONFIGURE_MSRS, 1)
    else
      AC_MSG_RESULT(no)
      AC_DEFINE(BX_CONFIGURE_MSRS, 0)
    fi
    ]
  )

AC_MSG_CHECKING(show IPS)
AC_ARG_ENABLE(show-ips,
  [  --enable-show-ips                 show IPS in Bochs log file],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SHOW_IPS, 1)
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SHOW_IPS, 0)
   fi],
  [
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SHOW_IPS, 1)
    ]
  )

AC_MSG_CHECKING(for use of .cpp as suffix)
AC_ARG_ENABLE(cpp,
  [  --enable-cpp                      use .cpp as C++ suffix],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    SUFFIX_LINE='.SUFFIXES: .cpp'
    CPP_SUFFIX='cpp'
   else
    AC_MSG_RESULT(no)
    SUFFIX_LINE='.SUFFIXES: .cc'
    CPP_SUFFIX='cc'
   fi],
  [
    AC_MSG_RESULT(no)
    SUFFIX_LINE='.SUFFIXES: .cc'
    CPP_SUFFIX='cc'
    ]
  )
AC_SUBST(SUFFIX_LINE)
AC_SUBST(CPP_SUFFIX)

if test "$enable_cpp" = yes; then
  echo "moving .cc source files to .cpp"
  sourcefiles=`find . -name "*.cc" -print`
  if test "$sourcefiles" != ""; then
    for ccname in $sourcefiles
    do
      cppname=`echo $ccname | sed -e "s/\.cc$/.cpp/"`
      echo "mv $ccname $cppname"
      mv   $ccname $cppname
    done
  else
    echo "no more .cc source files to rename"
  fi
fi



AC_MSG_CHECKING(for Bochs internal debugger support)
AC_ARG_ENABLE(debugger,
  [  --enable-debugger                 compile in support for Bochs internal debugger],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_DEBUGGER, 1)
    DEBUGGER_VAR='$(DEBUGGER_LIB)'
    NO_FLEX_YACC=''
    AC_PATH_PROG(FLEX, flex, not_found)
    AC_PATH_PROG(YACC, yacc, not_found)
    AC_MSG_CHECKING(whether to generate parser / lexer files)
    if test "$FLEX" = not_found -o "$YACC" = not_found -o "$MSVC_TARGET" = 1; then
      NO_FLEX_YACC='#'
      AC_MSG_RESULT(no)
    else
      AC_MSG_RESULT(yes)
    fi
    AC_SUBST(NO_FLEX_YACC)
    bx_debugger=1
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_DEBUGGER, 0)
    DEBUGGER_VAR=''
    bx_debugger=0
   fi
   ],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_DEBUGGER, 0)
    DEBUGGER_VAR=''
    bx_debugger=0
    ]
  )
AC_SUBST(DEBUGGER_VAR)

AC_MSG_CHECKING(for disassembler support)
AC_ARG_ENABLE(disasm,
  [  --enable-disasm                   compile in support for disassembler],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_DISASM, 1)
    DISASM_VAR='$(DISASM_LIB)'
   else
    AC_MSG_RESULT(no)
    if test "$bx_debugger" = 1; then
      echo "ERROR: debugger is enabled, so --enable-disasm is required"
      exit 1
    fi
    AC_DEFINE(BX_DISASM, 0)
    DISASM_VAR=''
   fi],
  [
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_DISASM, 1)
    DISASM_VAR='$(DISASM_LIB)'
  ])
AC_SUBST(DISASM_VAR)

AC_MSG_CHECKING(enable Bochs internal debugger GUI)
AC_ARG_ENABLE(debugger-gui,
  [  --enable-debugger-gui             compile in support for Bochs internal debugger GUI],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_DEBUGGER_GUI, 1)
    gui_debugger=1
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_DEBUGGER_GUI, 0)
    gui_debugger=0
   fi],
  [
    if test "$bx_debugger" = 1; then
      AC_MSG_RESULT(yes)
      AC_DEFINE(BX_DEBUGGER_GUI, 1)
      gui_debugger=1
    else
      AC_MSG_RESULT(no)
      AC_DEFINE(BX_DEBUGGER_GUI, 0)
      gui_debugger=0
    fi
    ]
  )

bx_gdb_stub=0
AC_MSG_CHECKING(for gdb stub enable)
AC_ARG_ENABLE(gdb-stub,
  [  --enable-gdb-stub                 enable gdb stub support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_GDBSTUB, 1)
    GDBSTUB_VAR='$(GDBSTUB_OBJS)'
    bx_gdb_stub=1
    if test "$bx_debugger" = 1; then
      AC_MSG_ERROR([[--enable-debugger and --enable-gdb-stub are mutually exclusive]])
    fi
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_GDBSTUB, 0)
    GDBSTUB_VAR=''
   fi],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_GDBSTUB, 0)
    GDBSTUB_VAR=''
    ]
  )
AC_SUBST(GDBSTUB_VAR)


AC_MSG_CHECKING(for I/O Interface to the debugger)
IODEBUG_OBJS=''
AC_ARG_ENABLE(iodebug,
  [  --enable-iodebug                  enable I/O interface to debugger],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_IODEBUG, 1)
    IODEBUG_OBJS='iodebug.o'
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_IODEBUG, 0)
   fi
   ],
  [
    if test "$bx_debugger" = 1; then
      # enable by default if debugger is on
      AC_MSG_RESULT(yes)
      AC_DEFINE(BX_SUPPORT_IODEBUG, 1)
      IODEBUG_OBJS='iodebug.o'
    else
      AC_MSG_RESULT(no)
    fi
    ]
  )
AC_SUBST(IODEBUG_OBJS)


AC_MSG_CHECKING(for ALL optimizations enabled)
AC_ARG_ENABLE(all-optimizations,
  [  --enable-all-optimizations        compile in all possible optimizations],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    speedups_all=1
   else
    AC_MSG_RESULT(no)
    speedups_all=0
   fi
   ],
  [
    AC_MSG_RESULT(no)
    speedups_all=0
    ]
  )

#
# Optimizations section.  Decide what the status of various optimizations
# should be based on configure choices and other factors.
#

if test "$speedups_all" = 1; then
  # Configure requested to force all options enabled.
  speedup_repeat=1
  speedup_fastcall=1
  speedup_handlers_chaining=1
fi

if test "$speedup_repeat" = 1; then
  AC_DEFINE(BX_SUPPORT_REPEAT_SPEEDUPS, 1)
else
  AC_DEFINE(BX_SUPPORT_REPEAT_SPEEDUPS, 0)
fi

if test "$speedup_fastcall" = 1; then
  AC_DEFINE(BX_FAST_FUNC_CALL, 1)
else
  AC_DEFINE(BX_FAST_FUNC_CALL, 0)
fi

if test "$bx_debugger" = 1 -a "$speedup_handlers_chaining" = 1; then
  speedup_handlers_chaining=0
  echo "ERROR: handlers-chaining speedups are not supported with internal debugger or gdbstub yet"
fi

if test "$bx_gdb_stub" = 1 -a "$speedup_handlers_chaining" = 1; then
  speedup_handlers_chaining=0
  echo "ERROR: handlers-chaining speedups are not supported with internal debugger or gdbstub yet"
fi

if test "$speedup_handlers_chaining" = 1; then
  AC_DEFINE(BX_SUPPORT_HANDLERS_CHAINING_SPEEDUPS, 1)
else
  AC_DEFINE(BX_SUPPORT_HANDLERS_CHAINING_SPEEDUPS, 0)
fi


READLINE_LIB=""
rl_without_curses_ok=no
rl_with_curses_ok=no

AC_MSG_CHECKING(whether user wants readline)
AC_ARG_ENABLE(readline,
  [  --enable-readline                 use readline library, if available],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
     want_readline=yes
   else
     AC_MSG_RESULT(no)
     want_readline=no
   fi],
  [
    AC_MSG_RESULT(yes)
    want_readline=yes
    ]
 )

use_readline=0
AC_MSG_CHECKING(whether to use readline)
if test "$want_readline" = yes; then
  if test "$bx_debugger" = 1; then

    AC_MSG_CHECKING(if readline works without -lcurses)
    OLD_LIBS=$LIBS
    LIBS="$LIBS -lreadline"
    AC_TRY_RUN([
      #include <stdio.h>
      #include <readline/readline.h>
      int main() { rl_initialize(); exit(0); }
      ],
      [ AC_MSG_RESULT(yes)
        rl_without_curses_ok=yes ],
      [ AC_MSG_RESULT(no) ]
    )
    AC_MSG_CHECKING(if readline works with -lcurses)
    LIBS="$LIBS -lcurses"
    AC_TRY_RUN([
      #include <stdio.h>
      #include <readline/readline.h>
      int main() { rl_initialize(); exit(0); }
      ],
      [AC_MSG_RESULT(yes)
       rl_with_curses_ok=yes ],
      [ AC_MSG_RESULT(no) ]
    )
    LIBS=$OLD_LIBS

    if test "$rl_without_curses_ok" = yes; then
      AC_MSG_RESULT(yes)
      AC_DEFINE(HAVE_LIBREADLINE, 1)
      READLINE_LIB="-lreadline"
      use_readline=1
    elif test "$rl_with_curses_ok" = yes; then
      AC_MSG_RESULT(yes)
      AC_DEFINE(HAVE_LIBREADLINE, 1)
      READLINE_LIB="-lreadline -lcurses"
      use_readline=1
    else
      AC_MSG_RESULT(no)
      echo WARNING: The readline library was disabled because it was not found.
    fi
  else
    AC_MSG_RESULT(no)
  fi
else
  AC_MSG_RESULT(no)
fi
AC_SUBST(READLINE_LIB)

AC_CHECK_HEADER(readline/history.h,
  AC_DEFINE(HAVE_READLINE_HISTORY_H)
  )

INSTRUMENT_DIR='instrument/stubs'

AC_MSG_CHECKING(for instrumentation support)
AC_ARG_ENABLE(instrumentation,
  [  --enable-instrumentation          compile in support for instrumentation],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_INSTRUMENTATION, 1)
    INSTRUMENT_VAR='$(INSTRUMENT_LIB)'
   elif test "$enableval" = no; then
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_INSTRUMENTATION, 0)
    INSTRUMENT_VAR=''
   else
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_INSTRUMENTATION, 1)
    INSTRUMENT_DIR=$enableval
    INSTRUMENT_VAR='$(INSTRUMENT_LIB)'
   fi],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_INSTRUMENTATION, 0)
    INSTRUMENT_VAR=''
    ]
  )
AC_SUBST(INSTRUMENT_DIR)
AC_SUBST(INSTRUMENT_VAR)

dnl // serial mode 'socket' needs wsock32.dll in non-plugin mode
if test "$bx_plugins" = 0; then
  case $target in
    *-pc-windows*)
      DEVICE_LINK_OPTS="$DEVICE_LINK_OPTS wsock32.lib"
      ;;
    *-cygwin* | *-mingw32*)
      DEVICE_LINK_OPTS="$DEVICE_LINK_OPTS -lwsock32"
      ;;
  esac
fi

AC_MSG_CHECKING(enable logging)
AC_ARG_ENABLE(logging,
  [  --enable-logging                  enable logging],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_NO_LOGGING, 0)
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_NO_LOGGING, 1)
   fi],
  [
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_NO_LOGGING, 0)
    ]
  )

AC_MSG_CHECKING(enable assert checks)
AC_ARG_ENABLE(assert-checks,
  [  --enable-assert-checks            enable BX_ASSERT checks],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_ASSERT_ENABLE, 1)
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_ASSERT_ENABLE, 0)
   fi],
  [
    if test "$bx_debugger" = 1; then
      AC_MSG_RESULT(yes)
      AC_DEFINE(BX_ASSERT_ENABLE, 1)
    else
      AC_MSG_RESULT(no)
      AC_DEFINE(BX_ASSERT_ENABLE, 0)
    fi
    ]
  )

AC_MSG_CHECKING(for raw serial support)
AC_ARG_ENABLE(raw-serial,
  [  --enable-raw-serial               use raw serial port access],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(USE_RAW_SERIAL, 1)
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(USE_RAW_SERIAL, 0)
   fi],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(USE_RAW_SERIAL, 0)
    ]
  )

AC_MSG_CHECKING(for CLGD54XX emulation)
AC_ARG_ENABLE(clgd54xx,
  [  --enable-clgd54xx                 enable CLGD54XX emulation],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_CLGD54XX, 1)
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_CLGD54XX, 0)
   fi],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_CLGD54XX, 0)
    ]
  )

support_fpu=1
AC_MSG_CHECKING(for FPU emulation)
FPU_VAR=''
AC_ARG_ENABLE(fpu,
  [  --enable-fpu                      compile in FPU emulation],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_FPU, 1)
    FPU_VAR='$(FPU_LIB)'
   elif test "$enableval" = no; then
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_FPU, 0)
    support_fpu=0
   else
    echo " "
    echo "ERROR: --enable-fpu does not accept a path"
    exit 1
   fi
   ],
  [
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_FPU, 1)
    FPU_VAR='$(FPU_LIB)'
    ]
  )
AC_SUBST(FPU_VAR)

support_vmx=0
AC_MSG_CHECKING(for VMX support)
AC_ARG_ENABLE(vmx,
  [  --enable-vmx                      VMX (virtualization extensions) emulation (--enable-vmx=[no|1|2])],
  [case "$enableval" in
    no | 0)
      support_vmx=0
      AC_MSG_RESULT(no)
      AC_DEFINE(BX_SUPPORT_VMX, 0)
      ;;
    yes | 1)
      support_vmx=1
      AC_MSG_RESULT(1)
      AC_DEFINE(BX_SUPPORT_VMX, 1)
      ;;
    2)
      support_vmx=2
      AC_MSG_RESULT(2)
      AC_DEFINE(BX_SUPPORT_VMX, 2)
      ;;
    *)
      echo "ERROR: --enable-vmx=$enableval not understood. Use --enable-vmx=[no|1|2]"
      exit 1
      ;;
   esac
   ],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_VMX, 0)
  ]
  )

if test "$support_vmx" -gt 0 -a "$bx_cpu_level" -lt 6; then
  AC_MSG_ERROR([for VMX support the CPU level must be set to 6])
fi

if test "$support_vmx" -gt 1 -a "$use_x86_64" = 0; then
  AC_MSG_ERROR([VMXx2 support require x86-64 enabled])
fi

support_svm=0
AC_MSG_CHECKING(for SVM support)
AC_ARG_ENABLE(svm,
  [  --enable-svm                      SVM (AMD: secure virtual machine) emulation],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_SVM, 1)
    support_svm=1
   elif test "$enableval" = no; then
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_SVM, 0)
   fi
   ],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_SVM, 0)
    ]
  )

if test "$support_svm" -gt 0 -a "$bx_cpu_level" -lt 6; then
  AC_MSG_ERROR([for SVM support the CPU level must be set to 6])
fi

if test "$support_svm" -gt 1 -a "$use_x86_64" = 0; then
  AC_MSG_ERROR([SVM support require x86-64 enabled])
fi

AC_MSG_CHECKING(for 3DNow! support)
AC_ARG_ENABLE(3dnow,
  [  --enable-3dnow                    3DNow! support (incomplete)],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_3DNOW, 1)
   elif test "$enableval" = no; then
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_3DNOW, 0)
   fi
   ],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_3DNOW, 0)
  ]
  )

AC_MSG_CHECKING(for alignment check support)
AC_ARG_ENABLE(alignment-check,
  [  --enable-alignment-check          alignment check (#AC) support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_ALIGNMENT_CHECK, 1)
   elif test "$enableval" = no; then
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_ALIGNMENT_CHECK, 0)
   fi
   ],
  [
    if test "$bx_cpu_level" -gt 3; then
      AC_MSG_RESULT(yes)
      AC_DEFINE(BX_SUPPORT_ALIGNMENT_CHECK, 1)
    else
      AC_MSG_RESULT(no)
      AC_DEFINE(BX_SUPPORT_ALIGNMENT_CHECK, 0)
    fi
    ]
  )

AC_MSG_CHECKING(for misaligned SSE support)
AC_ARG_ENABLE(misaligned-sse,
  [  --enable-misaligned-sse           misaligned SSE support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_MISALIGNED_SSE, 1)
   elif test "$enableval" = no; then
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_MISALIGNED_SSE, 0)
   fi
   ],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_MISALIGNED_SSE, 0)
    ]
  )

AC_MSG_CHECKING(for MONITOR/MWAIT instructions support (experimental))
AC_ARG_ENABLE(monitor_mwait,
  [  --enable-monitor-mwait            support for MONITOR/MWAIT instructions (experimental)],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_MONITOR_MWAIT, 1)
   elif test "$enableval" = no; then
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_MONITOR_MWAIT, 0)
   fi
   ],
  [
    if test "$bx_cpu_level" -gt 5; then
      AC_MSG_RESULT(yes)
      AC_DEFINE(BX_SUPPORT_MONITOR_MWAIT, 1)
    else
      AC_MSG_RESULT(no)
      AC_DEFINE(BX_SUPPORT_MONITOR_MWAIT, 0)
    fi
  ]
  )

support_avx=0
AC_MSG_CHECKING(for AVX instructions support)
AC_ARG_ENABLE(avx,
  [  --enable-avx                      support for AVX instructions],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_AVX, 1)
    support_avx=1
   elif test "$enableval" = no; then
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_AVX, 0)
   fi
   ],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_AVX, 0)
  ]
  )

if test "$support_avx" -gt 0 -a "$use_x86_64" = 0; then
  AC_MSG_ERROR([for AVX support x86-64 emulation must be compiled in!])
fi

AC_MSG_CHECKING(for x86 debugger support)
AC_ARG_ENABLE(x86-debugger,
  [  --enable-x86-debugger             x86 debugger support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_X86_DEBUGGER, 1)
   elif test "$enableval" = no; then
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_X86_DEBUGGER, 0)
   fi
   ],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_X86_DEBUGGER, 0)
    ]
  )

AC_MSG_CHECKING(for CDROM support)
AC_ARG_ENABLE(cdrom,
  [  --enable-cdrom                    CDROM support],
  [if test "$enableval" = no; then
     bx_cdrom=0
   else
     bx_cdrom=1
   fi],
  [ bx_cdrom=1 ]
  )

if test "$bx_cdrom" = 1; then
  AC_MSG_RESULT(yes)
  CDROM_OBJS='cdrom.o'
  case $target in
    *-pc-windows* | *-pc-winnt* | *-pc-cygwin* | *-mingw32*)
      CDROM_OBJS="$CDROM_OBJS cdrom_win32.o"
      AC_DEFINE(BX_SUPPORT_CDROM, 1)
      ;;
    *)
      AC_CHECK_HEADER(IOKit/storage/IOCDMedia.h,
        can_use_osx_cdrom=yes
        )
      if test "$with_amigaos" = yes; then
        # use the amiga cdrom file instead.
        CDROM_OBJS="$CDROM_OBJS cdrom_amigaos.o"
      elif test "$can_use_osx_cdrom" = yes; then
        # use cdrom_osx
        AC_MSG_RESULT(Using OSX IOKit CD Interface)
        CDROM_OBJS="$CDROM_OBJS cdrom_osx.o"
        EXTRA_LINK_OPTS="${EXTRA_LINK_OPTS} -framework IOKit -framework CoreFoundation "'$(GUI_LINK_OPTS)'
      else
        CDROM_OBJS="$CDROM_OBJS cdrom_misc.o"
      fi
      AC_DEFINE(BX_SUPPORT_CDROM, 1)
      ;;
  esac
else
  AC_MSG_RESULT(no)
  CDROM_OBJS=''
  AC_DEFINE(BX_SUPPORT_CDROM, 0)
fi

AC_SUBST(CDROM_OBJS)


SOUNDCARD_OBJS=''
SOUNDLOW_OBJS=''
GAME_OBJS=''
soundcard_present=0
gameport_present=0
AC_MSG_CHECKING(for Sound Blaster 16 support)
AC_ARG_ENABLE(sb16,
  [  --enable-sb16                     Sound Blaster 16 Support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
     SOUNDCARD_OBJS='sb16.o'
     soundcard_present=1
     AC_DEFINE(BX_SUPPORT_SB16, 1)
     GAME_OBJS='gameport.o'
     gameport_present=1
   else
     AC_MSG_RESULT(no)
     AC_DEFINE(BX_SUPPORT_SB16, 0)
   fi],

  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_SB16, 0)
    ]
  )

AC_MSG_CHECKING(for ES1370 soundcard support)
AC_ARG_ENABLE(es1370,
  [  --enable-es1370                     enable ES1370 soundcard support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    if test "$pci" != "1"; then
      AC_MSG_ERROR([ES1370 soundcard requires PCI support])
    fi
    SOUNDCARD_OBJS="$SOUNDCARD_OBJS es1370.o"
    soundcard_present=1
    AC_DEFINE(BX_SUPPORT_ES1370, 1)
    GAME_OBJS='gameport.o'
    gameport_present=1
   else
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_ES1370, 0)
   fi],
  [
    AC_MSG_RESULT(no)
    AC_DEFINE(BX_SUPPORT_ES1370, 0)
    ]
  )

if test "$soundcard_present" = 1; then
  AC_MSG_CHECKING(for sound lowlevel module)
  AC_DEFINE(BX_SUPPORT_SOUNDLOW, 1)
  case "$target" in
    *-linux* | *-freebsd*)
      SOUNDLOW_OBJS='soundlnx.o'
      AC_DEFINE(BX_SOUND_LOWLEVEL_C, bx_sound_linux_c)
      AC_MSG_RESULT(linux)
      bx_have_alsa=0
      AC_CHECK_HEADER([alsa/asoundlib.h], [bx_have_alsa=1])
      if test "$bx_have_alsa" = 1; then
        AC_DEFINE(BX_HAVE_ALSASOUND, 1)
        if test "$bx_plugins" = 1; then
          SOUND_LINK_OPTS="$SOUND_LINK_OPTS -lasound"
        else
          DEVICE_LINK_OPTS="$DEVICE_LINK_OPTS -lasound"
        fi
      fi
      ;;
    *-pc-windows*)
      SOUNDLOW_OBJS='soundwin.o'
      AC_DEFINE(BX_SOUND_LOWLEVEL_C, bx_sound_windows_c)
      DEVICE_LINK_OPTS="$DEVICE_LINK_OPTS winmm.lib"
      AC_MSG_RESULT(win)
      ;;
    *-cygwin* | *-mingw32*)
      SOUNDLOW_OBJS='soundwin.o'
      AC_DEFINE(BX_SOUND_LOWLEVEL_C, bx_sound_windows_c)
      DEVICE_LINK_OPTS="$DEVICE_LINK_OPTS -lwinmm"
      AC_MSG_RESULT(win)
      ;;
    *-macosx* | *-macos* | *-apple-darwin*)
      SOUNDLOW_OBJS='soundosx.o'
      AC_DEFINE(BX_SOUND_LOWLEVEL_C, bx_sound_osx_c)
      DEVICE_LINK_OPTS="$DEVICE_LINK_OPTS -framework CoreServices -framework AudioUnit -framework AudioToolbox"
      AC_MSG_RESULT(osx)
      ;;
    *)
      SOUNDLOW_OBJS=''
      AC_DEFINE(BX_SOUND_LOWLEVEL_C, bx_sound_lowlevel_c)
      AC_MSG_RESULT(dummy)
      ;;
  esac
  AC_MSG_CHECKING(for additional sound lowlevel modules)
  if test "$with_sdl" = yes; then
    SOUNDLOW_OBJS="$SOUNDLOW_OBJS soundsdl.o"
    if test "$bx_plugins" = 1; then
      SOUND_LINK_OPTS="$SOUND_LINK_OPTS `sdl-config --libs`"
    fi
    AC_MSG_RESULT(sdl)
  else
    AC_MSG_RESULT(none)
  fi
  if test "$bx_plugins" = 0; then
    SOUND_LIB_VAR='iodev/sound/libsound.a'
    AC_SUBST(SOUND_LIB_VAR)
  fi
else
  AC_DEFINE(BX_SUPPORT_SOUNDLOW, 0)
fi

AC_SUBST(SOUNDCARD_OBJS)
AC_SUBST(SOUNDLOW_OBJS)
AC_SUBST(SOUND_LINK_OPTS)


AC_MSG_CHECKING(for standard PC gameport support)
AC_ARG_ENABLE(gameport,
  [  --enable-gameport                 enable standard PC gameport support],
  [if test "$enableval" = yes; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(BX_SUPPORT_GAMEPORT, 1)
    if test "$gameport_present" = 0; then
      GAME_OBJS='gameport.o'
      case $target in
        *-cygwin* | *-mingw32*)
           DEVICE_LINK_OPTS="$DEVICE_LINK_OPTS -lwinmm"
           ;;
      esac
    fi
   else
    AC_MSG_RESULT(no)
    GAME_OBJS=''
    AC_DEFINE(BX_SUPPORT_GAMEPORT, 0)
   fi],
  [
    if test "$gameport_present" = 1; then
      AC_DEFINE(BX_SUPPORT_GAMEPORT, 1)
      AC_MSG_RESULT(yes)
    else
      AC_DEFINE(BX_SUPPORT_GAMEPORT, 0)
      AC_MSG_RESULT(no)
    fi]
  )
AC_SUBST(GAME_OBJS)


AC_PATH_PROG(DOCBOOK2HTML, docbook2html, not_found)
AC_MSG_CHECKING(whether to build docbook documentation)
build_docbook=0
AC_ARG_ENABLE(docbook,
  [  --enable-docbook                  build the Docbook documentation],
  [
    if test "$enableval" = yes; then build_docbook=1; fi
  ],
  [
    dnl Not specified. If they have docbook2html, then yes.
    if test "$DOCBOOK2HTML" != not_found; then build_docbook=1; fi
    ]
  )

if test $build_docbook = 1; then
  AC_MSG_RESULT(yes)
  BUILD_DOCBOOK_VAR=build_docbook
  INSTALL_DOCBOOK_VAR=install_docbook
  CLEAN_DOCBOOK_VAR=clean_docbook
  if test "$DOCBOOK2HTML" = not_found; then
    echo "ERROR: docbook2html must be installed to build the documentation"
    exit 1
  fi
else
  AC_MSG_RESULT(no)
fi
AC_SUBST(BUILD_DOCBOOK_VAR)
AC_SUBST(INSTALL_DOCBOOK_VAR)
AC_SUBST(CLEAN_DOCBOOK_VAR)
AC_SUBST(DOCBOOK2HTML)

PRIMARY_TARGET='box'
PLUGIN_LIBNAME_TRANSFORMATION='%.o=libbx_%.la'

if test "$bx_plugins" = 0; then
  HDIMAGE_LIB_VAR='iodev/hdimage/libhdimage.a'
  AC_SUBST(HDIMAGE_LIB_VAR)
fi
IODEV_LIB_VAR='iodev/libiodev.a'
NONINLINE_VAR='$(NONINLINE_OBJS)'

AC_SUBST(IODEV_LIB_VAR)
AC_SUBST(EXTRA_BX_OBJS)
AC_SUBST(NONINLINE_VAR)
AC_SUBST(INLINE_VAR)
AC_SUBST(EXTERNAL_DEPENDENCY)
AC_SUBST(RC_CMD)


AC_ARG_WITH(x11,
  [  --with-x11                        use X11 GUI],
  )

AC_ARG_WITH(win32,
  [  --with-win32                      use Win32 GUI],
  )

AC_ARG_WITH(macos,
  [  --with-macos                      use Macintosh/CodeWarrior environment],
  )

AC_ARG_WITH(carbon,
  [  --with-carbon                     compile for MacOS X with Carbon GUI],
  )

AC_ARG_WITH(nogui,
  [  --with-nogui                      no native GUI, just use blank stubs],
  )

AC_ARG_WITH(term,
  [  --with-term                       textmode terminal environment],
  )

AC_ARG_WITH(rfb,
  [  --with-rfb                        use RFB protocol, works with VNC viewer],
  )

AC_ARG_WITH(amigaos,
  [  --with-amigaos                    use AmigaOS (or MorphOS) GUI],
  )

AC_ARG_WITH(sdl,
  [  --with-sdl                        use SDL libraries],
  )

AC_ARG_WITH(svga,
  [  --with-svga                       use SVGALib libraries],
  )

AC_ARG_WITH(wx,
  [  --with-wx                         use wxWidgets libraries],
  )

AC_ARG_WITH(all-libs,
  [  --with-all-libs                   compile all guis that Bochs supports],
  )

dnl // DASH is option prefix for your platform
dnl // SLASH is directory for your platform
dnl // CXXFP is C++ File Prefix; the flag that tells the compiler
dnl //   this is a C++ source file
dnl // CFP is C File Prefix; the flag that tells the compiler
dnl //   this is a C source file
dnl // OFP is Object File Prefix; the flag that tells the compiler
dnl //   generate an object file with this name
DASH="-"
SLASH="/"
CXXFP=""
CFP=""
OFP="-o "
MAKELIB="ar rv \$@"
RMCOMMAND="rm -f "
# first define link variables without libtool
LINK="\$(CXX) -o \$@ \$(CXXFLAGS) \$(LDFLAGS)"
LINK_CONSOLE="\$(CXX) -o \$@ \$(CXXFLAGS_CONSOLE) \$(LDFLAGS)"
# unless NO_LT is set, add libtool to the front of the link command
if test "$NO_LT" != 1; then
  LINK="\$(LIBTOOL) --mode=link --tag CXX $LINK"
  LINK_CONSOLE="\$(LIBTOOL) --mode=link --tag CXX $LINK_CONSOLE"
fi
EXE=""
COMMAND_SEPARATOR="&& \\"
CD_UP_ONE="echo done"
CD_UP_TWO="echo done"
CD_UP_THREE="echo done"

INSTALL_TARGET=install_unix
INSTALL_LIST_FOR_PLATFORM=

with_wx=no

AC_MSG_CHECKING(for default gui on this platform)
AC_MSG_RESULT($DEFAULT_GUI)

display_libs=""

# the $with_* variable tells the gui library to use, but does NOT necessarily
# indicate the platform.  Settings that depend on the platform should be
# handled later.

check_xpm=0
AC_DEFINE(BX_WITH_X11, 0)
AC_DEFINE(BX_WITH_SDL, 0)
AC_DEFINE(BX_WITH_SVGA, 0)
AC_DEFINE(BX_WITH_RFB, 0)
AC_DEFINE(BX_WITH_AMIGAOS, 0)
AC_DEFINE(BX_WITH_WIN32, 0)
AC_DEFINE(BX_WITH_MACOS, 0)
AC_DEFINE(BX_WITH_CARBON, 0)
AC_DEFINE(BX_WITH_TERM, 0)
AC_DEFINE(BX_WITH_WX, 0)
AC_DEFINE(BX_WITH_NOGUI, 1)

SPECIFIC_GUI_OBJS="$SPECIFIC_GUI_OBJS \$(GUI_OBJS_NOGUI)"

AC_MSG_CHECKING(for display libraries)
AC_MSG_RESULT($display_libs)

AC_DEFINE(BX_USE_TEXTCONFIG, 1)

if test "$bx_plugins" = 1; then
  # every item in this list will be compiled as a plugin. Do not include
  # support objs; they will be mentioned in plugin-specific makefile rules.
  GUI_PLUGIN_OBJS="$GUI_PLUGIN_OBJS $SPECIFIC_GUI_OBJS"
else
  # include support objs
  GUI_NON_PLUGIN_OBJS="$GUI_NON_PLUGIN_OBJS $SPECIFIC_GUI_OBJS $SPECIFIC_GUI_SUPPORT_OBJS"
fi

AC_SUBST(GUI_CFLAGS)
AC_SUBST(GUI_CXXFLAGS)

AC_CHECK_PROGS(WGET, wget curl fetch, not_found)
if test "$WGET" = not_found; then
  AC_MSG_WARN([cannot find wget (or equivalent) command])
fi
# everybody has to re-invent the wheel. differently.
test "${WGET}" = "curl" && WGET="curl -O"

# modify settings based on target platform
case "$target" in
  *-macosx*)
    AC_DEFINE(BX_HAVE_STRDUP, 0)
    ;;
  *-macos*)
    AC_DEFINE(BX_HAVE_STRDUP, 0)
    AC_DEFINE(BX_PLUGINS, 0)
    AC_DEFINE(BX_HAVE_DLFCN_H, 0)
    AC_DEFINE(BX_HAVE_SOCKLEN_T, 0)
    AC_DEFINE(BX_HAVE_SELECT, 0)
    AC_DEFINE(BX_HAVE_GETTIMEOFDAY, 0)
    AC_DEFINE(BX_NO_ATTRIBUTES, 1)
    AC_DEFINE(BX_NETMOD_TUNTAP, 0)
    AC_DEFINE(BX_NETMOD_TAP, 0)
    ;;
  *-pc-windows*)
    INSTALL_TARGET='install_win32'
    CC="cl"
    CXX="$CC"
    #C_OPT="/Zi"   # for debugging
    C_OPT="/Ox"   # optimize for speed
    if test "$bx_plugins" = 1; then
      CFLAGS="/nologo /MD /W3 /EHs-c- /DNDEBUG /DWIN32 /D_WINDOWS /D_CRT_SECURE_NO_WARNINGS $C_OPT"
    else
      CFLAGS="/nologo /MT /W3 /EHs-c- /DNDEBUG /DWIN32 /D_WINDOWS /D_CRT_SECURE_NO_WARNINGS $C_OPT"
    fi
    CXXFLAGS="$CFLAGS"
    DASH="/"
    SLASH="\\"
    CXXFP="/Tp"
    CFP="/Tc"
    OFP="/Fo"
    MAKE=nmake
    MAKELIB="lib /nologo /subsystem:console /verbose /out:\$@"
    RMCOMMAND="-del"
    RANLIB="echo"
    #L_OPT="/debug"    # for debugging
    L_OPT=""           # no debug info
    LINK="link $L_OPT /nologo /subsystem:console /incremental:no /out:\$@ "
    LINK_CONSOLE=$LINK
    EXE=".exe"
    PRIMARY_TARGET="bochs.exe"
    BXIMAGE_LINK_OPTS="user32.lib"
    # also compile niclist if networking is on
    if test "$networking" = yes; then
      PRIMARY_TARGET="$PRIMARY_TARGET niclist.exe"
    fi
    COMMAND_SEPARATOR=""
    CD_UP_ONE="cd .."
    CD_UP_TWO="cd ..\.."
    CD_UP_THREE="cd ..\..\.."
    have_gettimeofday=0  # even though it may exist in build environment
    AC_DEFINE(BX_64BIT_CONSTANTS_USE_LL, 0)
    AC_DEFINE(inline, __forceinline)
    AC_DEFINE(BX_NO_ATTRIBUTES, 1)
    AC_DEFINE(HAVE_LIBREADLINE, 0)
    AC_DEFINE(HAVE_READLINE_HISTORY_H, 0)
    READLINE_LIB="";
    AC_DEFINE(BX_HAVE_DLFCN_H, 0)
    AC_DEFINE(BX_NETMOD_TAP, 0)
    ;;
  *-pc-cygwin*)
    EXE=".exe"
    PRIMARY_TARGET="bochs.exe"
    BXIMAGE_LINK_OPTS="-luser32"
    if test "$networking" = yes; then
      PRIMARY_TARGET="$PRIMARY_TARGET niclist.exe"
    fi
    AC_DEFINE(BX_HAVE_SELECT, 1)
    ;;
  *-pc-mingw*)
    BXIMAGE_LINK_OPTS="-luser32"
    if test "$networking" = yes; then
      PRIMARY_TARGET="$PRIMARY_TARGET niclist"
    fi
    AC_DEFINE(BX_HAVE_SELECT, 1)
    ;;
esac

ENH_DBG_OBJS=""
if test "$gui_debugger" = 1; then
  if test "$needs_gtk2" = 1; then
     ENH_DBG_OBJS="gtk_enh_dbg_osdep.o"
  elif test "$DEFAULT_GUI" = win32; then
     ENH_DBG_OBJS="win32_enh_dbg_osdep.o"
  fi
fi
AC_SUBST(ENH_DBG_OBJS)

DIALOG_OBJS=""
EXPORT_DYNAMIC="-export-dynamic"
case $target in
  *-pc-windows* | *-pc-winnt*)
    if test "$bx_plugins" = 1; then
      # set variables for building DLL plugins
      PLUGIN_VAR=""
      PLUGIN_LIBNAME_TRANSFORMATION='%.o=bx_%.dll'
      INSTALL_PLUGINS_VAR=install_dll_plugins
      LIBS="$LIBS advapi32.lib comctl32.lib comdlg32.lib gdi32.lib shell32.lib user32.lib"
    else
      if test "$with_win32" != yes; then
        LIBS="$LIBS comctl32.lib"
      fi
    fi
    DIALOG_OBJS="win32dialog.o win32paramdlg.o"
    EXPORT_DYNAMIC=""
    ;;
  *-cygwin* | *-mingw*)
    if test "$bx_plugins" = 1; then
      AC_CHECK_LIB(mingwex, opendir, LIBS="$LIBS -lmingwex")
      # set variables for building DLL plugins
      PRIMARY_TARGET=".win32_dll_plugin_target"
      PLUGIN_LIBNAME_TRANSFORMATION='%.o=bx_%.dll'
      INSTALL_PLUGINS_VAR=install_dll_plugins
    else
      if test "$with_win32" != yes; then
        LIBS="$LIBS -lcomctl32"
      fi
    fi
    DIALOG_OBJS="win32dialog.o win32paramdlg.o"
    EXPORT_DYNAMIC=""
    ;;
esac
AC_SUBST(DIALOG_OBJS)
AC_SUBST(EXPORT_DYNAMIC)

if test "$use_curses" = yes -a "$cross_configure" = 0; then
  AC_CHECK_LIB(curses, mvaddch, GUI_LINK_OPTS_TERM='-lcurses')
  AC_CHECK_LIB(ncurses, mvaddch, GUI_LINK_OPTS_TERM='-lncurses')
  AC_CHECK_LIB(termlib, mvaddch, GUI_LINK_OPTS_TERM='-ltermlib')
  AC_CHECK_LIB(pdcurses, mvaddch, GUI_LINK_OPTS_TERM='-lpdcurses')
  if test "$GUI_LINK_OPTS_TERM" = ""; then
    echo Curses library not found: tried curses, ncurses, termlib and pdcurses.
    exit 1
  fi
fi

if test "$with_term" = yes; then
  old_LIBS="$LIBS"
  LIBS="$LIBS $GUI_LINK_OPTS_TERM"
  AC_CHECK_FUNCS(color_set, AC_DEFINE(BX_HAVE_COLOR_SET, 1))
  AC_CHECK_FUNCS(mvhline, AC_DEFINE(BX_HAVE_MVHLINE, 1))
  AC_CHECK_FUNCS(mvvline, AC_DEFINE(BX_HAVE_MVVLINE, 1))
  LIBS="$old_LIBS"
fi

if test "$with_rfb" = yes; then
  # we need the socket function
  case $target in
    *-pc-windows*)
      # is this okay without a check ?
      RFB_LIBS="$RFB_LIBS wsock32.lib"
      have_socket=yes
      ;;
    *-pc-winnt* | *-pc-cygwin* | *-mingw32*)
      # is this okay without a check ?
      RFB_LIBS="$RFB_LIBS -lwsock32"
      have_socket=yes
      ;;
    *)
      AC_CHECK_FUNCS(socket, have_socket=yes)
      if test "$have_socket" != yes; then
        AC_CHECK_LIB(socket, socket,
          [
            RFB_LIBS="$RFB_LIBS -lsocket"
            have_socket=yes
          ])
      fi
      ;;
  esac
  if test "$have_socket" != yes; then
    echo 'ERROR: socket function required for RFB compile'
    exit 1
  fi
fi

# The ACX_PTHREAD function was written by
# Steven G. Johnson <stevenj@alum.mit.edu> and
# Alejandro Forero Cuervo <bachue@bachue.com>
# I found it in the ac-archive project on Source Forge.

AC_DEFUN([ACX_PTHREAD], [
AC_REQUIRE([AC_CANONICAL_HOST])
acx_pthread_ok=no

# First, check if the POSIX threads header, pthread.h, is available.
# If it isn't, don't bother looking for the threads libraries.
AC_CHECK_HEADER(pthread.h, , acx_pthread_ok=noheader)

# We must check for the threads library under a number of different
# names; the ordering is very important because some systems
# (e.g. DEC) have both -lpthread and -lpthreads, where one of the
# libraries is broken (non-POSIX).

# First of all, check if the user has set any of the PTHREAD_LIBS,
# etcetera environment variables, and if threads linking works using
# them:
if test x"$PTHREAD_LIBS$PTHREAD_CFLAGS" != x; then
	save_CFLAGS="$CFLAGS"
	CFLAGS="$CFLAGS $PTHREAD_CFLAGS"
	CXXFLAGS="$CXXFLAGS $PTHREAD_CFLAGS"
	save_LIBS="$LIBS"
	LIBS="$PTHREAD_LIBS $LIBS"
	AC_MSG_CHECKING([for pthread_join in LIBS=$PTHREAD_LIBS with CFLAGS=$PTHREAD_CFLAGS])
	AC_TRY_LINK_FUNC(pthread_join, acx_pthread_ok=yes)
	AC_MSG_RESULT($acx_pthread_ok)
	if test x"$acx_pthread_ok" = xno; then
		PTHREAD_LIBS=""
		PTHREAD_CFLAGS=""
	fi
	LIBS="$save_LIBS"
	CFLAGS="$save_CFLAGS"
fi

# Create a list of thread flags to try.  Items starting with a "-" are
# C compiler flags, and other items are library names, except for "none"
# which indicates that we try without any flags at all.

acx_pthread_flags="pthreads none -Kthread -kthread lthread -pthread -pthreads -mthreads pthread --thread-safe -mt"

# The ordering *is* (sometimes) important.  Some notes on the
# individual items follow:

# pthreads: AIX (must check this before -lpthread)
# none: in case threads are in libc; should be tried before -Kthread and
#       other compiler flags to prevent continual compiler warnings
# -Kthread: Sequent (threads in libc, but -Kthread needed for pthread.h)
# -kthread: FreeBSD kernel threads (preferred to -pthread since SMP-able)
# lthread: LinuxThreads port on FreeBSD (also preferred to -pthread)
# -pthread: Linux/gcc (kernel threads), BSD/gcc (userland threads)
# -pthreads: Solaris/gcc
# -mthreads: Mingw32/gcc, Lynx/gcc
# -mt: Sun Workshop C (may only link SunOS threads [-lthread], but it
#      doesn't hurt to check since this sometimes defines pthreads too;
#      also defines -D_REENTRANT)
# pthread: Linux, etcetera
# --thread-safe: KAI C++

case "${host_cpu}-${host_os}" in
	*solaris*)

	# On Solaris (at least, for some versions), libc contains stubbed
	# (non-functional) versions of the pthreads routines, so link-based
	# tests will erroneously succeed.  (We need to link with -pthread or
	# -lpthread.)  (The stubs are missing pthread_cleanup_push, or rather
	# a function called by this macro, so we could check for that, but
	# who knows whether they'll stub that too in a future libc.)  So,
	# we'll just look for -pthreads and -lpthread first:

	acx_pthread_flags="-pthread -pthreads pthread -mt $acx_pthread_flags"
	;;
esac

if test x"$acx_pthread_ok" = xno; then
for flag in $acx_pthread_flags; do

	case $flag in
		none)
		AC_MSG_CHECKING([whether pthreads work without any flags])
		;;

		-*)
		AC_MSG_CHECKING([whether pthreads work with $flag])
		PTHREAD_CFLAGS="$flag"
		;;

		*)
		AC_MSG_CHECKING([for the pthreads library -l$flag])
		PTHREAD_LIBS="-l$flag"
		;;
	esac

	save_LIBS="$LIBS"
	save_CFLAGS="$CFLAGS"
	LIBS="$PTHREAD_LIBS $LIBS"
	CFLAGS="$CFLAGS $PTHREAD_CFLAGS"

	# Check for various functions.  We must include pthread.h,
	# since some functions may be macros.  (On the Sequent, we
	# need a special flag -Kthread to make this header compile.)
	# We check for pthread_join because it is in -lpthread on IRIX
	# while pthread_create is in libc.  We check for pthread_attr_init
	# due to DEC craziness with -lpthreads.  We check for
	# pthread_cleanup_push because it is one of the few pthread
	# functions on Solaris that doesn't have a non-functional libc stub.
	# We try pthread_create on general principles.
	AC_TRY_LINK([#include <pthread.h>],
		    [pthread_t th; pthread_join(th, 0);
		     pthread_attr_init(0); pthread_cleanup_push(0, 0);
		     pthread_create(0,0,0,0); pthread_cleanup_pop(0); ],
		    [acx_pthread_ok=yes])

	LIBS="$save_LIBS"
	CFLAGS="$save_CFLAGS"

	AC_MSG_RESULT($acx_pthread_ok)
	if test "x$acx_pthread_ok" = xyes; then
		break;
	fi

	PTHREAD_LIBS=""
	PTHREAD_CFLAGS=""
done
fi

# Various other checks:
if test "x$acx_pthread_ok" = xyes; then
	save_LIBS="$LIBS"
	LIBS="$PTHREAD_LIBS $LIBS"
	save_CFLAGS="$CFLAGS"
	CFLAGS="$CFLAGS $PTHREAD_CFLAGS"

	# Detect AIX lossage: threads are created detached by default
	# and the JOINABLE attribute has a nonstandard name (UNDETACHED).
	AC_MSG_CHECKING([for joinable pthread attribute])
	AC_TRY_LINK([#include <pthread.h>],
		    [int attr=PTHREAD_CREATE_JOINABLE;],
		    ok=PTHREAD_CREATE_JOINABLE, ok=unknown)
	if test x"$ok" = xunknown; then
		AC_TRY_LINK([#include <pthread.h>],
			    [int attr=PTHREAD_CREATE_UNDETACHED;],
			    ok=PTHREAD_CREATE_UNDETACHED, ok=unknown)
	fi
	if test x"$ok" != xPTHREAD_CREATE_JOINABLE; then
		AC_DEFINE(PTHREAD_CREATE_JOINABLE, $ok,
			  [Define to the necessary symbol if this constant
			   uses a non-standard name on your system.])
	fi
	AC_MSG_RESULT(${ok})
	if test x"$ok" = xunknown; then
		AC_MSG_WARN([we do not know how to create joinable pthreads])
	fi

	AC_MSG_CHECKING([if more special flags are required for pthreads])
	flag=no
	case "${host_cpu}-${host_os}" in
		*-aix* | *-freebsd*)     flag="-D_THREAD_SAFE";;
		*solaris* | alpha*-osf*) flag="-D_REENTRANT";;
	esac
	AC_MSG_RESULT(${flag})
	if test "x$flag" != xno; then
		PTHREAD_CFLAGS="$flag $PTHREAD_CFLAGS"
	fi

	LIBS="$save_LIBS"
	CFLAGS="$save_CFLAGS"

	# More AIX lossage: must compile with cc_r
	AC_CHECK_PROG(PTHREAD_CC, cc_r, cc_r, ${CC})
else
	PTHREAD_CC="$CC"
fi

AC_SUBST(PTHREAD_LIBS)
AC_SUBST(PTHREAD_CFLAGS)
AC_SUBST(PTHREAD_CC)

# Finally, execute ACTION-IF-FOUND/ACTION-IF-NOT-FOUND:
if test x"$acx_pthread_ok" = xyes; then
	ifelse([$1],,AC_DEFINE(HAVE_PTHREAD,1,[Define if you have POSIX threads libraries and header files.]),[$1])
	:
else
	acx_pthread_ok=no
	$2
fi

])dnl ACX_PTHREAD

pthread_ok=no
ACX_PTHREAD([
  pthread_ok=yes
  #echo Using PTHREAD_LIBS=$PTHREAD_LIBS
  #echo Using PTHREAD_CFLAGS=$PTHREAD_CFLAGS
  #echo Using PTHREAD_CC=$PTHREAD_CC
  ])


# since some features need the pthread library, check that it was found.
# But on win32 platforms, the pthread library is not needed.
if test "$cross_configure" = 0; then
  if test "$with_rfb" = yes -o "$soundcard_present" = 1; then
    if test "$pthread_ok" = yes; then
      if test "$with_rfb" = yes; then
        RFB_LIBS="$RFB_LIBS $PTHREAD_LIBS"
      fi
      if test "$soundcard_present" = 1; then
        if test "$bx_plugins" = 1; then
          SOUND_LINK_OPTS="$SOUND_LINK_OPTS $PTHREAD_LIBS"
        else
          DEVICE_LINK_OPTS="$DEVICE_LINK_OPTS $PTHREAD_LIBS"
        fi
      fi
      CFLAGS="$CFLAGS $PTHREAD_CFLAGS"
      CXXFLAGS="$CXXFLAGS $PTHREAD_CFLAGS"
      CC="$PTHREAD_CC"
    else
      case "$target" in
        *-pc-windows* | *-pc-winnt* | *-cygwin* | *-mingw32*)
          # pthread not needed for win32 platform
          ;;
        *)
          echo ERROR: the pthread library is required, but could not be found.; exit 1
      esac
    fi
  fi
fi

dnl // DEPRECATED configure options - force users to remove them

AC_MSG_CHECKING(for APIC support (deprecated))
AC_ARG_ENABLE(apic,
  [  --enable-apic                     enable APIC support (deprecated)],
  [AC_MSG_RESULT($enableval)
   AC_MSG_ERROR([DEPRECATED - enabled automatically with SMP or when CPU_LEVEL >= 5])
  ],
  [
   AC_MSG_RESULT(no)
  ])

AC_MSG_CHECKING(for X2APIC support (deprecated))
AC_ARG_ENABLE(x2apic,
  [  --enable-x2apic                   enable X2APIC support (deprecated)],
  [AC_MSG_RESULT($enableval)
   AC_MSG_ERROR([DEPRECATED - moved to .bochsrc options])
  ],
  [
   AC_MSG_RESULT(no)
  ])

AC_MSG_CHECKING(for VESA BIOS extensions (deprecated))
AC_ARG_ENABLE(vbe,
  [  --enable-vbe                      use VESA BIOS extensions (deprecated)],
  [AC_MSG_RESULT($enableval)
   AC_MSG_ERROR([DEPRECATED - this feature always available with .bochsrc option])
  ],
  [
   AC_MSG_RESULT(no)
  ])

AC_MSG_CHECKING(for ACPI extensions support (deprecated))
AC_ARG_ENABLE(acpi,
  [  --enable-acpi                     use ACPI extensions support (deprecated)],
  [AC_MSG_RESULT($enableval)
   AC_MSG_ERROR([DEPRECATED - this feature enabled with PCI configure option and could be turned on through .bochsrc option])
  ],
  [
   AC_MSG_RESULT(no)
  ])

AC_MSG_CHECKING(for instruction trace cache support (deprecated))
AC_ARG_ENABLE(trace-cache,
  [  --enable-trace-cache              support instruction trace cache (deprecated)],
  [AC_MSG_RESULT($enableval)
   AC_MSG_ERROR([DEPRECATED - this feature will be enabled in all configurations])
  ],
  [
   AC_MSG_RESULT(no)
  ])

# Create some subdirectories for when you run configure from some other
# directory.
if test ! -d instrument; then mkdir instrument; fi
if test ! -d build; then mkdir build; fi
if test ! -d build/linux; then mkdir build/linux; fi
if test ! -d build/win32; then mkdir build/win32; fi
if test ! -d build/win32/nsis; then mkdir build/win32/nsis; fi

AC_SUBST(INSTALL_TARGET)
AC_SUBST(INSTALL_LIST_FOR_PLATFORM)
AC_SUBST(RFB_LIBS)
AC_SUBST(GUI_OBJS)
AC_SUBST(DEVICE_LINK_OPTS)
AC_SUBST(GUI_LINK_OPTS)
AC_SUBST(GUI_LINK_OPTS_TERM)
AC_SUBST(GUI_LINK_OPTS_WX)
AC_SUBST(BXIMAGE_LINK_OPTS)
AC_SUBST(DASH)
AC_SUBST(SLASH)
AC_SUBST(CXXFP)
AC_SUBST(CFP)
AC_SUBST(OFP)
AC_SUBST(MAKELIB)
AC_SUBST(RMCOMMAND)
AC_SUBST(LINK)
AC_SUBST(LINK_CONSOLE)
AC_SUBST(EXE)
AC_SUBST(PRIMARY_TARGET)
AC_SUBST(PLUGIN_LIBNAME_TRANSFORMATION)
AC_SUBST(COMMAND_SEPARATOR)
AC_SUBST(CD_UP_ONE)
AC_SUBST(CD_UP_TWO)
AC_SUBST(CD_UP_THREE)
AC_SUBST(VERSION)
AC_SUBST(VER_STRING)
AC_SUBST(WIN_VER_STRING)
AC_SUBST(REL_STRING)
AC_SUBST(EXTRA_LINK_OPTS)
AC_SUBST(GUI_NON_PLUGIN_OBJS)
AC_SUBST(GUI_PLUGIN_OBJS)
AC_SUBST(IODEV_NON_PLUGIN_OBJS)
AC_SUBST(IODEV_PLUGIN_OBJS)
AC_SUBST(IODEV_EXT_NON_PLUGIN_OBJS)
AC_SUBST(IODEV_EXT_PLUGIN_OBJS)
AC_SUBST(PLUGIN_VAR)
AC_SUBST(PLUGIN_TARGET)
AC_SUBST(INSTALL_PLUGINS_VAR)

AC_PATH_PROG(GZIP, gzip)
AC_PATH_PROG(TAR, tar)

AC_OUTPUT(Makefile cpu/Makefile cpu/cpudb/Makefile \
          fpu/Makefile elf/Makefile \
	  runtime/Makefile disasm/Makefile bxversion.h)
# gui/Makefile disasm/Makefile ${INSTRUMENT_DIR}/Makefile misc/Makefile \
# fpu/Makefile doc/docbook/Makefile build/linux/bochs-dlx \
# bxversion.h bxversion.rc build/macosx/Info.plist \
# build/win32/nsis/Makefile build/win32/nsis/bochs.nsi \
# host/linux/pcidev/Makefile memory/Makefile \
# iodev/Makefile )
